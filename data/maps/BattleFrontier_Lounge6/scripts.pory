const REF_TRADE = INGAME_TRADE_MEOWTH
const REQUESTED_MON = SPECIES_SKITTY
const OFFERED_MON = SPECIES_MEOWTH
const TRADE_FLAG = FLAG_BATTLE_FRONTIER_TRADE_DONE

raw`
BattleFrontier_Lounge6_MapScripts::
    .byte 0
`

script BattleFrontier_Lounge6_EventScript_Trader
{
    lock
    faceplayer
    // Check if trade has already been completed
    if (flag(TRADE_FLAG))
    {
        goto(BattleFrontier_Lounge6_EventScript_TradeCompleted)
    }
    // Buffer trade data
    setvar(VAR_0x8008, REF_TRADE)
    setvar(VAR_0x8004, REF_TRADE)
    specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
    copyvar(VAR_0x8009, VAR_RESULT)
    // Ask player if they want to trade
	bufferspeciesname(STR_VAR_1, REQUESTED_MON)
	bufferspeciesname(STR_VAR_2, OFFERED_MON)
    msgbox(BattleFrontier_Lounge6_Text_WouldYouLikeToTrade, MSGBOX_YESNO)
    if (var(VAR_RESULT) == NO) // Player declined to trade
    {
        goto(BattleFrontier_Lounge6_EventScript_DeclineTrade)
    }
    // Have player select a party member for trade
    special(ChoosePartyMon)
    waitstate
    copyvar(VAR_0x800A, VAR_0x8004)
    if(var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) // Nothing was chosen
    {
        goto(BattleFrontier_Lounge6_EventScript_DeclineTrade)
    }
    // Check player selection against requested mon
    copyvar(VAR_0x8005, VAR_0x800A)
    specialvar(VAR_RESULT, GetTradeSpecies)
    copyvar(VAR_0x800B, VAR_RESULT)
    goto_if_ne(VAR_RESULT, VAR_0x8009, BattleFrontier_Lounge6_EventScript_NotRequestedMon) // Selection didn't match request
    // Setup and do trade
    copyvar(VAR_0x8004, VAR_0x8008)
    copyvar(VAR_0x8005, VAR_0x800A)
    special(CreateInGameTradePokemon)
    special(DoInGameTradeScene)
    waitstate
    // Buffer species names for NPC text
    bufferspeciesname(STR_VAR_2, OFFERED_MON)
    msgbox(BattleFrontier_Lounge6_Text_PromiseIllBeGoodToIt, MSGBOX_DEFAULT)
    // Indicate trade is complete and exit script
    setflag(TRADE_FLAG)
    release
    end
}

script BattleFrontier_Lounge6_EventScript_DeclineTrade
{
	msgbox (BattleFrontier_Lounge6_Text_WellThatsFineToo, MSGBOX_DEFAULT)
#ifdef TRADE_PORTRAITS
	call(BattleFrontier_Loung6_EventScript_TryAddRequestToPokedex)
#endif
	release
	end
}

script BattleFrontier_Lounge6_EventScript_NotRequestedMon
{
    bufferspeciesname(STR_VAR_1, REQUESTED_MON)
    msgbox(BattleFrontier_Lounge6_Text_DontTradeForAnythingButMon, MSGBOX_DEFAULT)
#ifdef TRADE_PORTRAITS
	call(BattleFrontier_Loung6_EventScript_TryAddRequestToPokedex)
#endif
    release
    end
}

script BattleFrontier_Lounge6_EventScript_TradeCompleted
{
    msgbox(BattleFrontier_Lounge6_Text_SkittySoMuchCuterThanImagined, MSGBOX_DEFAULT)
    release
    end
}

script BattleFrontier_Loung6_EventScript_TryAddRequestToPokedex
{
    // Check if reqeusted mon has been seen. If not, set the "seen" flag for it in the dex
    setvar(VAR_0x8004, REQUESTED_MON)
    specialvar(VAR_RESULT, GetSeenPokedexFlag)
    if(!(var(VAR_RESULT)))
    {
        bufferspeciesname(STR_VAR_1, REQUESTED_MON)
        msgbox(format("Oh, let me at least point out what a {STR_VAR_1} is."), MSGBOX_DEFAULT)
        // Show requested mon's portrait to player
        showmonpic(REQUESTED_MON, 10, 3)
        msgbox(format("This is what a {STR_VAR_1} looks like if I remember correctly."), MSGBOX_DEFAULT)
        special(SetSeenPokedexFlag)
        hidemonpic
		msgbox(format("Good luck dearie!"))
    }
    return
}

text BattleFrontier_Lounge6_Text_WouldYouLikeToTrade
{
	"My POKÃ©MON is a {STR_VAR_2}.\n"
	"Do you know it?\l"
	"It's quite cute and rather nice.\p"
	"This little one, I could trade with\n"
	"pride!\p"
	"Would you like to trade me a {STR_VAR_1}\n"
	"for my {STR_VAR_2}?"
}

text BattleFrontier_Lounge6_Text_PromiseIllBeGoodToIt
{
	"Oh, it's adorable!\n"
	"Thank you!\l"
	"I promise I'll be good to it!\p"
	"Oh! I hope you'll be good to\n"
	"my {STR_VAR_2}, too!"
}

text BattleFrontier_Lounge6_Text_DontTradeForAnythingButMon
{
	"Oh, I'm sorry!\n"
	"I don't intend to trade for anything\l"
	"but a {STR_VAR_1}."
}

text BattleFrontier_Lounge6_Text_WellThatsFineToo
{
	"Oh, you won't?\n"
	"Well, that's fine, too.\l"
	"Please come visit us again."
}

text BattleFrontier_Lounge6_Text_SkittySoMuchCuterThanImagined
{
	"Giggle!\n"
	"A SKITTY is so much cuter than I had\l"
	"imagined. I'm delighted!"
}
