const REF_TRADE = INGAME_TRADE_PLUSLE
const REQUESTED_MON = SPECIES_VOLBEAT
const OFFERED_MON = SPECIES_PLUSLE

raw `
FortreeCity_House1_MapScripts::
    .byte 0
`

script FortreeCity_House1_EventScript_Trader
{
    lock
    faceplayer
    // Check if trade has already been completed
    if (flag(FLAG_FORTREE_NPC_TRADE_COMPLETED))
    {
        goto(FortreeCity_House1_EventScript_TradeCompleted)
    }
    // Buffer trade data
    setvar(VAR_0x8008, REF_TRADE)
    setvar(VAR_0x8004, REF_TRADE)
    specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
    copyvar(VAR_0x8009, VAR_RESULT)
    // Ask player if they want to trade
    msgbox(FortreeCity_House1_Text_YouWillTradeWontYou, MSGBOX_YESNO)
    if (var(VAR_RESULT) == NO) // Player declined to trade
    {
        goto(FortreeCity_House1_EventScript_DeclineTrade)
    }
    // Have player select a party member for trade
    special(ChoosePartyMon)
    waitstate
    copyvar(VAR_0x800A, VAR_0x8004)
    if(var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) // Nothing was chosen
    {
        goto(FortreeCity_House1_EventScript_DeclineTrade)
    }
    // Check player selection against requested mon
    copyvar(VAR_0x8005, VAR_0x800A)
    specialvar(VAR_RESULT, GetTradeSpecies)
    copyvar(VAR_0x800B, VAR_RESULT)
    goto_if_ne(VAR_RESULT, VAR_0x8009, FortreeCity_House1_EventScript_NotRequestedMon) // Selection didn't match request
    // Setup and do trade
    copyvar(VAR_0x8004, VAR_0x8008)
    copyvar(VAR_0x8005, VAR_0x800A)
    special(CreateInGameTradePokemon)
    special(DoInGameTradeScene)
    waitstate
    // Buffer species names for NPC text
    bufferspeciesname(STR_VAR_1, REQUESTED_MON)
    bufferspeciesname(STR_VAR_2, OFFERED_MON)
    msgbox(FortreeCity_House1_Text_MonYouTakeCare, MSGBOX_DEFAULT)
    // Indicate trade is complete and exit script
    setflag(FLAG_FORTREE_NPC_TRADE_COMPLETED)
    release
    end
}


script FortreeCity_House1_EventScript_TradeCompleted
{
    msgbox(FortreeCity_House1_Text_GoingToMakeVolbeatStrong, MSGBOX_DEFAULT)
    release
    end
}

script FortreeCity_House1_EventScript_DeclineTrade
{
    msgbox(FortreeCity_House1_Text_YouWontTradeMe, MSGBOX_DEFAULT)
#ifdef TRADE_PORTRAITS
    call(FortreeCity_House1_EventScript_TryAddRequestToPokedex)
#endif
    release
    end
}

script FortreeCity_House1_EventScript_NotRequestedMon
{
    bufferspeciesname(STR_VAR_1, REQUESTED_MON)
    msgbox(FortreeCity_House1_Text_ThisIsntAMon, MSGBOX_DEFAULT)
#ifdef TRADE_PORTRAITS
    call(FortreeCity_House1_EventScript_TryAddRequestToPokedex)
#endif
    release
    end
}

script FortreeCity_House1_EventScript_TryAddRequestToPokedex
{
    // Check if player has already seen requested mon
    setvar(VAR_0x8004, REQUESTED_MON)
    specialvar(VAR_RESULT, GetSeenPokedexFlag)
    if(!(var(VAR_RESULT))) // Requested mon hasn't been seen yet
    {
        msgbox(format("At least let me show you what one looks like before you go!"), MSGBOX_DEFAULT)
        // Show requested mon portrait and give some flavor text
        showmonpic(REQUESTED_MON, 10, 3)
        bufferspeciesname (STR_VAR_1, REQUESTED_MON)
        msgbox(format("This is what {STR_VAR_1} looks like.\pYou can always check your POKéDEX if you need a refresher."), MSGBOX_DEFAULT)
        // Set the seen flag for the requested mon and hide the portrait
        special(SetSeenPokedexFlag)
        hidemonpic
    }
    return // Go back to calling script1
}

script FortreeCity_House1_EventScript_ExpertF
{
    msgbox(FortreeCity_House1_Text_TradingMemoriesWithOthers, MSGBOX_NPC)
    end
}

script FortreeCity_House1_EventScript_Zigzagoon
{
    lock
    faceplayer
    waitse
    playmoncry(SPECIES_ZIGZAGOON, CRY_MODE_NORMAL)
    msgbox(FortreeCity_House1_Text_Zigzagoon, MSGBOX_DEFAULT)
    waitmoncry
    release
    end
}

text FortreeCity_House1_Text_YouWillTradeWontYou
{
	"Wrooooaaar! I need it!\n"
	"I have to get me a {STR_VAR_1}!\l"
	"I'll do anything for it!\p"
	"…Uh… Did you hear that?\n"
	"My shout from the bottom of my heart?\p"
	"Having heard that, you will trade\n"
	"your {STR_VAR_1} for my {STR_VAR_2},\l"
	"won't you?"
}

text FortreeCity_House1_Text_MonYouTakeCare
{
	"Oh, yeah, right on!\p"
	"{STR_VAR_1}, welcome!\n"
	"{STR_VAR_2}, you take care!"
}

text FortreeCity_House1_Text_ThisIsntAMon
{
	"Uh, no, I don't think so.\n"
	"That isn't a {STR_VAR_1}."
}

text FortreeCity_House1_Text_YouWontTradeMe
{
	"No? You won't trade me?\n"
	"Even after I bared my heart to you?"
}

text FortreeCity_House1_Text_GoingToMakeVolbeatStrong
{
	"I'm going to make VOLBEAT super\n"
	"strong from this moment on!\p"
	"I hope you do the same with PLUSLE!"
}

text FortreeCity_House1_Text_TradingMemoriesWithOthers
{
	"Trading POKéMON with others…\p"
	"It's as if you're trading your own\n"
	"memories with other people."
}

text FortreeCity_House1_Text_Zigzagoon
{
	"ZIGZAGOON: Gumomoh?"
}
