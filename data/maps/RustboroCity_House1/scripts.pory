const REF_TRADE = INGAME_TRADE_SEEDOT
const REQUESTED_MON = SPECIES_RALTS
const OFFERED_MON = SPECIES_SEEDOT

raw `
RustboroCity_House1_MapScripts::
    .byte 0
`

script RustboroCity_House1_EventScript_Trader
{
    lock
    faceplayer
    if(flag(FLAG_RUSTBORO_NPC_TRADE_COMPLETED) == TRUE)
    {
        goto (RustboroCity_House1_EventScript_TradeCompleted)
    }
    setvar(VAR_0x8008, REF_TRADE)
    setvar(VAR_0x8004, REF_TRADE)
    specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
    copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(RustboroCity_House1_Text_IllTradeIfYouWant, MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO)
    {
        goto(RustboroCity_House1_EventScript_DeclineTrade)
    }
    special(ChoosePartyMon)
    waitstate
    copyvar(VAR_0x800A, VAR_0x8004)
    if(var(VAR_0x8004) == PARTY_NOTHING_CHOSEN)
    {
        goto(RustboroCity_House1_EventScript_DeclineTrade)
    }
    copyvar(VAR_0x8005, VAR_0x800A)
    specialvar(VAR_RESULT, GetTradeSpecies)
    copyvar(VAR_0x800B, VAR_RESULT)
    goto_if_ne(VAR_RESULT, VAR_0x8009, RustboroCity_House1_EventScript_NotRequestedMon)
    copyvar(VAR_0x8004, VAR_0x8008)
    copyvar(VAR_0x8005, VAR_0x800A)
    special(CreateInGameTradePokemon)
    special(DoInGameTradeScene)
    waitstate
    msgbox(RustboroCity_House1_Text_PleaseBeGoodToMyPokemon, MSGBOX_DEFAULT)
    setflag(FLAG_RUSTBORO_NPC_TRADE_COMPLETED)
    release
    end
}

script RustboroCity_House1_EventScript_TradeCompleted
{
	msgbox(RustboroCity_House1_Text_AnyPokemonCanBeCute, MSGBOX_DEFAULT)
	release
	end
}

script RustboroCity_House1_EventScript_DeclineTrade
{
    msgbox(RustboroCity_House1_Text_YouDontWantToThatsOkay, MSGBOX_DEFAULT)
#ifdef TRADE_PORTRAITS
    call(RustboroCity_House1_EventScript_TryAddRequestToPokedex)
#endif
    release
    end
}

script RustboroCity_House1_EventScript_NotRequestedMon
{
    bufferspeciesname(STR_VAR_1, REQUESTED_MON)
    msgbox(RustboroCity_House1_Text_DoesntLookLikeMonToMe, MSGBOX_DEFAULT)
#ifdef TRADE_PORTRAITS
    call(RustboroCity_House1_EventScript_TryAddRequestToPokedex)
#endif
    release
    end
}

script RustboroCity_House1_EventScript_TryAddRequestToPokedex
{
    // Check if reqeusted mon has been seen. If not, set the "seen" flag for it in the dex
    setvar(VAR_0x8004, REQUESTED_MON)
    specialvar(VAR_RESULT, GetSeenPokedexFlag)
    if(!(var(VAR_RESULT)))
    {
        msgbox(format("Maybe as an incentive I can show you what you're looking for."), MSGBOX_DEFAULT)
        // Show requested mon's portrait to player
        showmonpic(REQUESTED_MON, 10, 3)
        bufferspeciesname(STR_VAR_1, REQUESTED_MON)
        msgbox(format("This is {STR_VAR_1}. Pretty cool, huh?"), MSGBOX_DEFAULT)
        special(SetSeenPokedexFlag)
        hidemonpic
    }
    return
}

script RustboroCity_House1_EventScript_Hiker
{
    msgbox(RustboroCity_House1_Text_AllSortsOfPlaces, MSGBOX_NPC)
    end
}

text RustboroCity_House1_Text_IllTradeIfYouWant
{
	"Huh? My POKéMON is cute?\n"
	"Sure, I knew that.\p"
	"But if you really want, I'm willing\n"
	"to trade it to you.\p"
	"I'll trade you my {STR_VAR_2} for\n"
	"a {STR_VAR_1} if you want."
}

text RustboroCity_House1_Text_PleaseBeGoodToMyPokemon
{
	"Eheheh…\n"
	"Please be good to my POKéMON."
}

text RustboroCity_House1_Text_DoesntLookLikeMonToMe
{
	"Huh? That doesn't look anything like\n"
	"a {STR_VAR_1} to me."
}

text RustboroCity_House1_Text_YouDontWantToThatsOkay
{
	"Oh, if you don't want to, that's okay.\n"
	"But my POKéMON is cute, you know…"
}

text RustboroCity_House1_Text_AnyPokemonCanBeCute
{
	"Any POKéMON can be cute if you raise\n"
	"it with care and kindness."
}

text RustboroCity_House1_Text_AllSortsOfPlaces
{
	"In all sorts of places, there are all\n"
	"sorts of POKéMON and people.\p"
	"I find that fascinating, so I go to all\n"
	"sorts of places."
}